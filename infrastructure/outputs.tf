# =============================================================================
# VM Outputs
# =============================================================================

output "nixos_vms" {
  description = "Map of NixOS VM information"
  value = merge(
    {
      for name, vm in proxmox_virtual_environment_vm.nixos_vm : name => {
        vm_id = vm.vm_id
        name  = vm.name
        ip    = local.hosts_config.hosts[name].ip
      }
    },
    {
      for name, vm in proxmox_virtual_environment_vm.nixos_gpu_vm : name => {
        vm_id = vm.vm_id
        name  = vm.name
        ip    = local.hosts_config.hosts[name].ip
      }
    }
  )
}

# =============================================================================
# Colmena Helper
# =============================================================================

output "colmena_hosts" {
  description = "NixOS hosts for Colmena deployment"
  value = {
    for name, host in local.hosts_config.hosts : name => {
      ip   = host.ip
      tags = host.tags
    }
  }
}

# =============================================================================
# DNS Configuration
# =============================================================================

output "unbound_config" {
  description = "Unbound DNS config for pfSense (copy to Services > DNS Resolver > Custom Options)"
  value = <<-EOT
# Generated by OpenTofu - paste into pfSense DNS Resolver Custom Options
# =============================================================================
# INFRASTRUCTURE (static)
# =============================================================================
# pfSense Router
local-zone: "pfsense.tap." static
local-data: "pfsense.tap. 3600 IN A 10.0.0.1"

# Pi-hole DNS
local-zone: "pihole.tap." static
local-data: "pihole.tap. 3600 IN A 10.0.0.18"

# Unraid NAS
local-zone: "tower.tap." static
local-data: "tower.tap. 3600 IN A 10.0.0.5"

# Proxmox Cluster
local-zone: "strongbad.tap." static
local-data: "strongbad.tap. 3600 IN A 10.0.0.10"

local-zone: "strongmad.tap." static
local-data: "strongmad.tap. 3600 IN A 10.0.0.11"

local-zone: "strongsad.tap." static
local-data: "strongsad.tap. 3600 IN A 10.0.0.12"

# =============================================================================
# NixOS VMs (from hosts.json)
# =============================================================================
%{for name, host in local.hosts_config.hosts~}
%{if try(host.domain, "") != ""~}
local-zone: "${host.domain}." redirect
local-data: "${host.domain}. 3600 IN A ${host.ip}"

%{endif~}
%{endfor~}
  EOT
}
